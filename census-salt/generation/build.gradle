/**
 * $ ./gradlew
 * The default task will build the project and run the test suite inside
 * your local spark environment (spark-submit must be on the PATH).
 */

buildscript {
 repositories {
   mavenLocal()
   mavenCentral()
   jcenter()
 }
 dependencies {
   classpath 'org.github.ngbinh.scalastyle:gradle-scalastyle-plugin_2.11:0.7.2'
 }
}

apply plugin: 'java'
apply plugin: 'scala'
apply plugin: 'maven'
apply plugin: 'idea'
apply plugin: 'scalaStyle'

group = 'software.uncharted.censushackathon2016'
version = '0.0.1'

project.ext {
  scalaBinaryVersion = '2.10'
  scalaVersion = '2.10.4'
  sparkVersion = '1.4.0'
  saltVersion = '2.0.0'
  sparkpipeVersion = '0.9.4'
}

repositories {
  mavenLocal()
  mavenCentral()
  jcenter()
  maven {url "http://oss.sonatype.org/content/groups/public/"}
}

jar {
  baseName = 'census-salt'
  version =  version
  dependsOn configurations.runtime
  from {
    (configurations.runtime - configurations.provided).collect {
      it.isDirectory() ? it : zipTree(it)
    }
  } {
    exclude "META-INF/*.SF"
    exclude "META-INF/*.DSA"
    exclude "META-INF/*.RSA"
  }
}

sourceCompatibility = 1.7
targetCompatibility = 1.7

configurations {
  provided
  compile.extendsFrom provided
}

dependencies {
  compile "software.uncharted.salt:salt-core:${saltVersion}"
  compile "software.uncharted.sparkpipe:sparkpipe-core:${sparkpipeVersion}"

  //scala
  provided("org.scala-lang:scala-library:${scalaVersion}")

  //spark
  provided "org.apache.spark:spark-core_${scalaBinaryVersion}:${sparkVersion}"
  provided "org.apache.spark:spark-sql_${scalaBinaryVersion}:${sparkVersion}"
  provided "org.scala-lang:scala-library:$scalaBinaryVersion"

  compile "com.databricks:spark-csv_2.10:1.2.0"

  compile "org.json:json:20151123"
  compile "com.amazonaws:aws-java-sdk:1.10.46"
  compile "org.spire-math:spire_2.11:0.11.0"
}

task wrapper(type: Wrapper) {
  gradleVersion = '2.10'
}

task run(overwrite: true, type: Exec, dependsOn: [assemble, scalaStyle]) {
  executable = 'spark-submit'
  args = ["--class","software.uncharted.censushackathon2016.Main","/opt/salt/build/libs/census-salt-${version}.jar", "/opt/data/*.csv", "/opt/output"]
}

idea {
  module {
    inheritOutputDirs = false
    outputDir = file("$buildDir/classes/main/")
  }
}

scalaStyle {
  configLocation = "scalastyle_config.xml"
  includeTestSourceDirectory = true
  source = sourceSets.main.allScala
  testSource = sourceSets.test.allScala
  failOnWarning = true
}

defaultTasks 'run'
